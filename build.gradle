plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.40'
    id "org.jetbrains.dokka" version "0.9.18"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.dcendents.android-maven" version "1.4.1"
    id "com.github.ben-manes.versions" version "0.21.0"
}

group = "dev.moetz"
version = "0.1.0"

repositories {
    mavenCentral()
    jcenter()
}

ext {
    okHttpVersion = "3.14.2"
    kotlinVersion = "1.3.40"
    mockkVersion = "1.9.3"
    kluentVersion = "1.50"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile("com.squareup.okhttp3:okhttp:$okHttpVersion")
    compile("com.squareup.okhttp3:logging-interceptor:$okHttpVersion")

    testCompile("junit:junit:4.12")
    testCompile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
    testCompile("org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion")
    testCompile("io.mockk:mockk:$mockkVersion")
    testCompile("org.amshove.kluent:kluent:$kluentVersion")
    testCompile("com.squareup.okhttp3:logging-interceptor:$okHttpVersion")
}


// publish stuff:
// publishing works with ./gradlew clean build test dokka sourcesJar bintrayUpload -PdryRun=true (in a dry-run, for the final publish set dryRun to false)

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('bintray.public.properties').newDataInputStream())
} catch (IOException e) {
    println("bintray.public.properties not found. This can be ignored, unless you want to publish. " + e.localizedMessage)
}


Properties secretProperties = new Properties()
try {
    secretProperties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (IOException e) {
    println("local.properties not found. This can be ignored, unless you want to publish. " + e.localizedMessage)
}


group = properties.getProperty("library.groupId")
version = properties.getProperty("library.version")

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId properties.getProperty("library.groupId")
            artifactId properties.getProperty("library.artifact")

            description properties.getProperty("library.description")
            url properties.getProperty("library.siteURL")

            licenses {
                license {
                    name properties.getProperty("license.name")
                    url properties.getProperty("license.url")
                }
            }
            scm {
                connection properties.getProperty("library.gitURL")
                developerConnection properties.getProperty("library.gitURL")
                url properties.getProperty("library.siteURL")
            }
        }
    }
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from kotlin.sourceSets.main.kotlin.srcDirs
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    classifier = 'javadoc'
    from fileTree(new File(project.buildDir, 'javadoc'))
}


artifacts {
    archives javadocJar
    archives sourcesJar
}


bintray {
    user = secretProperties.getProperty("bintray.user")
    key = secretProperties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = properties.getProperty("bintray.repo")
        name = properties.getProperty("bintray.name")
        desc = properties.getProperty("library.description")
        websiteUrl = properties.getProperty("library.siteURL")
        vcsUrl = properties.getProperty("library.gitURL")
        licenses = [properties.getProperty("license.allLicenses")]
        dryRun = true
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = properties.getProperty("library.description")
        }
    }
}